#Requires AutoHotkey v2.0 
#include clsCommonShape.ahk2
#include ..\clsShapes.ahk2

;----------------------------------------------------------------------
; Body related shapes
;----------------------------------------------------------------------
Class ToolShape extends commonShape {
   /**
    * Additional Tool shape Startup for drawing
    */
   Startup() {
      super.Startup()
      GDIp.SetPenDashStyle(this.pPrime, 1)
      this.Gui.PIC.GetPos(&this.pX, &this.pY, &this.pW, &this.pH)
      ; Slicer brush
      this.fBrush := this.myBrushes.Add(0x88808080)
      this.bPen   := this.myPens.Add(0xFF000000 | this.Color, this.shapeThickness)
   }
   /**
    * Make adjustments based on the passed in factor, call before startup
    * @param Factor 
    */
   Init(*) {
      Super.Init()
      Switch this.Shape {
         Case Shapes.SHAPE_CROP :
            this.Color := "FUCHSIA"
         Case Shapes.SHAPE_OCR :
            this.Color := "Orange"
         Default :
      }
      this.shapeThickness := this.DPIFactor * 2 ; thin selection
   }  
    /**
    * Draw a new tool shape at X/Y & W/H
    * @param G Graphics Handle
    * @param X X Location of upper left
    * @param Y Y Location of upper left
    * @param W Width of shape to draw
    * @param H Height of shape to draw
    */
   Draw(G, X, Y, W, H) {
      Switch this.Shape {
         Case Shapes.SHAPE_SLICE, Shapes.SHAPE_EXTEND :
            If (W > H) {
               ; Vert Rect
               Y := this.pY, H := this.pH
            } Else {
               ; Horz Rect
               X := this.pX, W := this.pW
            }
            GDIp.FillRectangle(G, this.fBrush, X, Y, W, H) ; zero upper/left corner always
            If (this.Shape = Shapes.SHAPE_EXTEND) {
               BW := Snips.BorderWidth / 2
               GDIp.DrawRectangle(G, this.pPrime, X - BW, Y - BW, W + BW, H + BW) ; zero upper/left corner always
            }

         Default :
            GDIp.DrawRectangle(G, this.bPen, X, Y, W, H) ; zero upper/left corner always
      }
      this.sCoord := { X1: X, X2: X + W, Y1: Y, Y2: Y + H, W: W, H: H, X: X, Y: Y }
   }
}
